-- Copyright 2018 Carnegie Mellon University. See Notice.txt
-- Distribution Statement A: Approved for Public Release; Distribution is Unlimited.
property set SecurityClassificationProperties is
	with EMV2; -- may want to reduce the all to specific component categories

	-- component is trusted
	Trusted : aadlboolean applies to (all);

-- Security Classifications
--
	-- The security clearance properties include a primary 
	-- security classification (e.g. Top Secret, Secret, Confidential)
	-- and an optional supplemental security statement. 
	--	
	SecurityClearance: aadlstring 
	applies to (device, system, bus, memory, processor,abstract);
	--
	SecurityClearanceSupplement: aadlstring
	applies to (device, system, bus, memory, processor,abstract);
	--
	-- The secondary security clearance is provided in the event of multiple clearances 
	-- (e.g. a clearance from two different agencies.) The secondary clearance also has a 
	-- primary security classification and an optional supplemental security statement.
	--
	-- No assumption is made about the relationship between the Security_Clearance property 
	-- and the Secondary_Security_Clearance property.
	--
	SecondarySecurityClearance: aadlstring 
	applies to (system, process, thread, data, processor, memory);
	--	
	SecondarySecurityClearanceSupplement: aadlstring
	applies to (system, process, thread, data, processor, memory);
	--
-- Information Security Levels
	--
	-- The information security level properties include a primary 
	-- security classification (e.g. Top Secret, Secret, Confidential)
	-- and a caveat (e.g. control markings) statement. 
	--
	-- There are no secondary information security levels or caveats.
	---
 	InformationSecurityLevel: aadlstring applies to (all);
 	--
    -- Information security caveats are treated as a single string.
	-- For example, a US information caveat value may be "//SI/TK//RELIDO"
	-- such that the combined level and caveats classification marking may be "TOP SECRET//SI/TK//RELIDO"
	--
	InformationSecurityCaveats: type aadlstring;


-- A security domain is the determining factor in the classification of an enclave of servers/computers. 
-- A network with a different security domain is kept separate from other networks. 
-- Examples: NIPRNet, SIPRNet. JWICS, NSANet are all kept separate.
-- Systems in a domain are tagged with the same SecurityDomain value 
	SecurityDomain: aadlstring applies to (all);


-- collection of application domains that the recipient is allowed to deal with. 
-- This can be certain types of information, e.g., personnel data, or 
-- certain types of system control, e.g., flight control vs. mission vs. entertainment
	NeedToKnowDomains: list of aadlstring applies to (all);


-- Vulnerabilities
		--
		-- There may be multiple vulnerabilities for each element; vulnerability is considered as a condition of the system.
		--
		-- This contains elements from the EMV2 hazards property, which may not be needed.
		--
	Vulnerabilities: list of record
  	( 
   Name : aadlstring;				-- short identification phrase for the vulnerability
   Description : aadlstring;      	-- description of the vulnerability
   CrossReference : aadlstring;   	-- cross reference to an external document
   Phases : list of aadlstring;   	-- operational phases in which the vulnerability may be exploited
   Environment : aadlstring;      	-- description of operational environment
   Threat : aadlstring;             -- description of the circumstances under which the vulnerability may be exploited
   Loss : aadlstring; 			    -- description of the loss that may result from exploitation of the vulnerability
   Risk : aadlstring;               -- description of risk; where risk is characterized by 
                                    -- severity, likelihood, and occurrence probability
   Severity : EMV2::SeverityRange ;             	-- actual risk as severity 
   Likelihood : EMV2::LikelihoodLabels;         	-- actual risk as likelihood/probability
   Probability: EMV2::ProbabilityRange;         	-- probability of a exploitation (i.e. realization of loss) 
 -- 
   AcceptableSeverity : EMV2::SeverityRange;        -- acceptable risk as severity 
   AcceptableLikelihood : EMV2::LikelihoodLabels;   -- acceptable risk as likelihood/probability
   DevelopmentAssuranceLevel : EMV2::DALLabels; 	-- level of rigor in development 
                                         
   VerificationMethod : aadlstring; 			-- verification method to address the vulnerability or threat
   SecurityReport : aadlstring;       			-- analysis/assessment of hazard
   Comment : aadlstring; 
   )
     applies to (all);
end SecurityClassificationProperties;